ZSH=${HOME}/dotfiles

case `uname` in
  Darwin)
    IS_OSX=1
    ;;
esac

source_if_exists () {
  if [ -f "$1" ]; then
    source "$1"
  else
    echo "Note: not sourcing $1"
  fi
}

# Source other things
if [ ! ${IS_OSX} ]; then
  source_if_exists /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
  source_if_exists /usr/share/zsh/plugins/zsh-history-substring-search/zsh-history-substring-search.zsh
fi

# Source csh compatibility functions
source ~/.config/csh_compat.sh

typeset -U config_files
config_files=(${ZSH}/**/*.zsh)

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source ${file}
done

# Change XTERM title
# TODO: re-enable to work with definition of precmd in vim.zsh
#case ${TERM} in
#  xterm*)
#    precmd () {print -Pn "\e]0;%n@%m: %~\a"}
#    ;;
#esac

autoload -Uz compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source ${file}
done
